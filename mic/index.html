<!DOCTYPE html>
<html>
	<head>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=no">
		<meta name="apple-mobile-web-app-status-bar-style" content="black-translucent">
		<meta name="apple-mobile-web-app-capable" content="yes">
		<meta name="mobile-web-app-capable" content="yes">
		<link rel="manifest" href="site.webmanifest">
		<link type="text/css" rel="stylesheet" href="style.css">

		<script id="vertexShader" type="x-shader/x-vertex">

			varying vec2 vUv;

			void main() {
				vUv = uv;
				gl_Position = vec4( position, 1.0 );
			}

		</script>

		<script id="frag01" type="x-shader/x-fragment">
			// see https://codepen.io/xander807/pen/xxwvwQv
			uniform sampler2D tAudioData;
			uniform sampler2D iChannel0;
			uniform float iTime;
			varying vec2 vUv;

			void main() {
				vec3 c;
				float z = 0.1 * iTime;
				vec2 uv = vUv;
				vec2 p = vUv - 0.5;
				float l = 0.2 * length(p);
				for (int i = 0; i < 3; i++) {
					z += 0.01;
					uv += p / l * (sin(z) + 1.0) * abs(sin(l * 7.0 - z * 2.3));
					c[i] = 0.005 / length(abs(mod(uv, 1.0) - 0.5));
				}
				float intensity = texture2D(tAudioData, vec2(l, 0.5)).x;
				gl_FragColor = vec4(c / l * intensity, iTime);
			}
		</script>
		<script id="frag02" type="x-shader/x-fragment">

			uniform sampler2D tAudioData;
			uniform sampler2D iChannel0;
			uniform float iTime;
			varying vec2 vUv;

			// 3 color modes
			#define COLOR_MODE 3 // 1-3
			
			#define AMP_FACT 0.2
			#define uRingsN 18
			#define uWidth 0.2
			#define uSize 0.2
			#define NOISE_AMP 0.2
			
			
			const vec4 kHue = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
			
			#define hue2rgb(h) clamp(abs(fract(vec3(h) + kHue.xyz) * 6.0 - kHue.www) - kHue.xxx, 0.0, 1.0)
			#define _hls2rgb(x,y,z) z + y * (clamp(abs(mod(x * 6.0 + vec3(0.0, 4.0, 2.0), 6.0) - 3.0) - 1.0, 0.0, 1.0) - 0.5) * (1.0 - abs(2.0 * z - 1.0))
			#define hsv2rgb(x,y,z) z * mix(kHue.xxx, clamp(abs(fract(vec3(x) + kHue.xyz) * 6.0 - kHue.www) - kHue.xxx, 0.0, 1.0), y)
			#define hsl2rgb(x,y,z) z * mix(kHue.xxx, clamp(abs(fract(vec3(x) + kHue.xyz) * 6.0 - kHue.www) - kHue.xxx, 0.0, 1.0), (1.0 - abs(2.0 * y - 1.0)))
			
			float waves(vec2 coord, vec2 coordMul1, vec2 coordMul2, vec2 phases, vec2 timeMuls) {
					return 0.5 * (sin(dot(coord, coordMul1) + timeMuls.x * iTime + phases.x) + cos(dot(coord, coordMul2) + timeMuls.y * iTime + phases.y));
			}
			
			const vec2 noiseDir1 = vec2(1.0, 0.0019);
			const vec2 noiseDir2 = vec2(1.0, 0.8000);
			
			float ringMultiplier(vec2 uv, float amp, float phase, float off, float power) {
				vec3 noise1 = texture2D(tAudioData, noiseDir1 * phase            ).rrr;
				vec3 noise2 = texture2D(iChannel0, noiseDir2 - noiseDir1 * phase).rgb;
				vec2 d = vec2(off, 0.5);
				d.x += 0.6 * waves(
					uv,
					vec2( 1.9 + 0.4 * noise1.r, 1.9 + 0.4 * noise1.g ) * 3.3,
					vec2( 5.7 + 1.4 * noise1.b, 5.7 + 1.4 * noise2.r ) * 2.8,
					vec2( noise1.r - noise2.r,  noise1.g + noise2.b  ) * 5.0,
					vec2( 1.1 )
				);
				d.y += 0.4 * waves(
					uv,
					vec2( -1.7 - 0.9 * noise2.g,  1.7 + 0.9 * noise2.b ) * 3.1,
					vec2(  5.9 + 0.8 * noise1.g, -5.9 - 0.8 * noise1.b ) * 3.7,
					vec2( noise1.g + noise2.g,    noise1.b - noise2.r  ) * 5.0,
					vec2( -0.9 )
				);
				float a = noise1.r * NOISE_AMP + 0.6 * (abs(d.x) + abs(d.y));
				vec2 duv = uv + normalize(d) * a * amp * AMP_FACT;
				return smoothstep( -power, power, pow(abs(length(duv) - uSize), noise1.r * noise2.r));
			}
			
			vec3 getColor(vec2 uv, float s){
				return vec3(
					dot(uv, vec2(cos(iTime + s), sin(iTime -s))),
					dot(uv, vec2(cos(iTime + 1.75* s),sin(iTime +s))),
					dot(uv, vec2(sin(iTime + s)))
				);
			}
			
			vec3 hue(vec2 uv, float h, float speed){
				return vec3(0.5) + hue2rgb(h + (dot(uv,vec2(0.5)) + iTime) * speed);  
			}
			

			void main() {
				vec2 uv = vec2( 0.5, 0.5 ) - vUv;
				vec3 color = vec3( 1.0 );
				float power = pow( uWidth * 0.1, uWidth );
				float size = uSize * 0.38;
				
		 #if (COLOR_MODE == 1)
				vec3 t1 = getColor(uv, 2.), t2 = 1. - getColor(uv, -2.1);
		 #elif (COLOR_MODE == 2)
				vec3 t1 = .65 - hue(uv, 0.6, 0.005), t2 = .72 - hue(uv.yx, 0.8, 0.05);
		 #elif (COLOR_MODE == 3)
				vec3 t1 = 1. - vec3(0.1, 0.5, 0.7), t2 = 1. - vec3(0.8, 0.1, 0.8);
		 #else
				vec3 t1 = 1. - vec3(0.9, 0.5, 0.3), t2 = 1. - vec3(0.8, 0.1, 0.2);
		 #endif
		 
				float xoff = 0.5 * (0.9 * cos(iTime * 0.6 + 1.1) + 0.4 * cos(iTime * 2.4));
				for (int i = 0; i < uRingsN; i++) {
					float frac = float(i) / float(uRingsN);
					float amp = ringMultiplier(uv,
																		 0.1 + pow(frac, 3.0) * 0.7,
																		 pow(1.0 - frac, uWidth) * 0.09 + iTime * 0.0001,
																		 xoff,
																		 power);
					color *= mix(mix(t1, t2, pow(frac, 3.0)), vec3(1.0), pow(amp, 2.5));
				}
				color = 1. - color;
				gl_FragColor = vec4(color, length(color));
			}
		</script>
		<script id="frag03" type="x-shader/x-fragment">
			
			uniform sampler2D tAudioData;
			uniform sampler2D iChannel0;
			uniform float iTime;
			varying vec2 vUv;
			
			// adapted from https://www.shadertoy.com/view/MdVSWG

			vec3 B2_spline(vec3 x) { // returns 3 B-spline functions of degree 2
				vec3 t = 3.0 * x;
				vec3 b0 = step(0.0, t)     * step(0.0, 1.0-t);
				vec3 b1 = step(0.0, t-1.0) * step(0.0, 2.0-t);
				vec3 b2 = step(0.0, t-2.0) * step(0.0, 3.0-t);
				return 0.5 * (
					b0 * pow(t, vec3(2.0)) +
					b1 * (-2.0*pow(t, vec3(2.0)) + 6.0*t - 3.0) + 
					b2 * pow(3.0-t,vec3(2.0))
				);
			}
			
			void main(){
				// create pixel coordinates
				vec2 uv = vUv;
			
				float fVBars = 100.;
				float fHSpacing = 1.00;
				
				float fHFreq = (uv.x * 3.14);
				float squarewave = sign(sin(fHFreq*fVBars)+1.0-fHSpacing);
				
				float x = floor(uv.x * fVBars)/fVBars;
				float fSample = texture2D( tAudioData, vec2(abs(2.0 * x - 1.0), 0.25)).x;
			  
				float fft = squarewave * fSample* 0.5;
				
				float fHBars = 100.0;
				float fVSpacing = 0.180;
				float fVFreq = (uv.y * 3.14);
				fVFreq = sign(sin(fVFreq * fHBars)+1.0-fVSpacing);
			
				vec2 centered = vec2(1.0) * uv - vec2(1.0);
				float t = iTime / 100.0;
				float polychrome = 1.0;
				vec3 spline_args = fract(vec3(polychrome*uv.x-t) + vec3(0.0, -1.0/3.0, -2.0/3.0));
				vec3 spline = B2_spline(spline_args);
				
				float f = abs(centered.y);
				vec3 base_color  = vec3(1.0, 1.0, 1.0) - f*spline;
				vec3 flame_color = pow(base_color, vec3(3.0));
				
				float tt = 0.3 - uv.y;
				float df = sign(tt);
				df = (df + 1.0)/0.5;
				vec3 col = flame_color * vec3(1.0 - step(fft, abs(0.3-uv.y))) * vec3(fVFreq);
				col -= col * df * 0.180;
				
				// output final color
				gl_FragColor = vec4(col,1.0);
			}
		</script>

	</head>
	<body>
		<div id="overlay">
    		<button id="startButton">Start</button>
		</div>
		<div id="container"></div>
		
		<script type="importmap">
			{
				"imports": {
					"three": "https://cdn.jsdelivr.net/npm/three@0.167.1/build/three.module.min.js",
					"three/addons/": "https://cdn.jsdelivr.net/npm/three@0.167.1/examples/jsm/"
				}
			}
		</script>
		<script type="module" src="main.js"></script>

	</body>
</html>