<!DOCTYPE html>
<html lang="en">
	<head>
		<title>three.js webaudio - visualizer</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=no">
		<meta name="apple-mobile-web-app-status-bar-style" content="black-translucent">
		<meta name="apple-mobile-web-app-capable" content="yes">
		<meta name="mobile-web-app-capable" content="yes">
		<link type="text/css" rel="stylesheet" href="style.css">

		<script id="vertexShader" type="x-shader/x-vertex">

			varying vec2 vUv;

			void main() {
				vUv = uv;
				gl_Position = vec4( position, 1.0 );
			}

		</script>

		<script id="fragmentShader" type="x-shader/x-fragment">

			uniform sampler2D tAudioData;
			varying vec2 vUv;

			void main() {
				vec3 b = vec3( 0.125, 0.125, 0.125 );
				float f = texture2D( tAudioData, vec2( vUv.x, 0.0 ) ).r;
				float i = step( vUv.y, f ) * step( f - 0.0125, vUv.y );
				vec3 c = vec3( 1.0, i, 0.0 );
				gl_FragColor = vec4( mix( b, c, i ), 1.0 );

			}

		</script>

		<script id="fragmentShader_new" type="x-shader/x-fragment">

			uniform sampler2D tAudioData;
			uniform float iTime;
			varying vec2 vUv;

			// 3 color modes
			#define COLOR_MODE 1 // 1-3
			
			#define AMP_FACT 0.2
			#define uRingsN 18
			#define uWidth 0.2
			#define uSize 0.2
			#define NOISE_AMP 0.2
			
			
			const vec4 kHue = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
			
			#define hue2rgb(h) clamp(abs(fract(vec3(h) + kHue.xyz) * 6.0 - kHue.www) - kHue.xxx, 0.0, 1.0)
			#define _hls2rgb(x,y,z) z + y * (clamp(abs(mod(x * 6.0 + vec3(0.0, 4.0, 2.0), 6.0) - 3.0) - 1.0, 0.0, 1.0) - 0.5) * (1.0 - abs(2.0 * z - 1.0))
			#define hsv2rgb(x,y,z) z * mix(kHue.xxx, clamp(abs(fract(vec3(x) + kHue.xyz) * 6.0 - kHue.www) - kHue.xxx, 0.0, 1.0), y)
			#define hsl2rgb(x,y,z) z * mix(kHue.xxx, clamp(abs(fract(vec3(x) + kHue.xyz) * 6.0 - kHue.www) - kHue.xxx, 0.0, 1.0), (1.0 - abs(2.0 * y - 1.0)))
			
			float waves(vec2 coord, vec2 coordMul1, vec2 coordMul2, vec2 phases, vec2 timeMuls) {
					return 0.5 * (sin(dot(coord, coordMul1) + timeMuls.x * iTime + phases.x) + cos(dot(coord, coordMul2) + timeMuls.y * iTime + phases.y));
			}
			
			const vec2 noiseDir1 = vec2(1.0, 0.0019);
			const vec2 noiseDir2 = vec2(1.0, 0.8000);
			
			float ringMultiplier(vec2 uv, float amp, float phase, float off, float power) {
				vec3 noise1 = texture2D(tAudioData, noiseDir1 * phase            ).rrr;
				vec3 noise2 = pow(vec3(2.0), texture2D(tAudioData, noiseDir2 - noiseDir1 * phase).rgb);
				vec2 d = vec2(off, 0.5);
				d.x += 0.6 * waves(
					uv,
					vec2( 1.9 + 0.4 * noise1.r, 1.9 + 0.4 * noise1.g ) * 3.3,
					vec2( 5.7 + 1.4 * noise1.b, 5.7 + 1.4 * noise2.r ) * 2.8,
					vec2( noise1.r - noise2.r,  noise1.g + noise2.b  ) * 5.0,
					vec2( 1.1 )
				);
				d.y += 0.4 * waves(
					uv,
					vec2( -1.7 - 0.9 * noise2.g,  1.7 + 0.9 * noise2.b ) * 3.1,
					vec2(  5.9 + 0.8 * noise1.g, -5.9 - 0.8 * noise1.b ) * 3.7,
					vec2( noise1.g + noise2.g,    noise1.b - noise2.r  ) * 5.0,
					vec2( -0.9 )
				);
				float a = noise1.r * NOISE_AMP + 0.6 * (abs(d.x) + abs(d.y));
				vec2 duv = uv + normalize(d) * a * amp * AMP_FACT;
				return smoothstep( -power, power, pow(abs(length(duv) - uSize), noise1.r * noise2.r));
			}
			
			vec3 getColor(vec2 uv, float s){
				return vec3(
					dot(uv, vec2(cos(iTime + s), sin(iTime -s))),
					dot(uv, vec2(cos(iTime + 1.75* s),sin(iTime +s))),
					dot(uv, vec2(sin(iTime + s)))
				);
			}
			
			vec3 hue(vec2 uv, float h, float speed){
				return vec3(0.5) + hue2rgb(h + (dot(uv,vec2(0.5)) + iTime) * speed);  
			}
			

			void main() {
				vec2 uv = vec2( 0.5, 0.260 ) - vUv;
				vec3 color = vec3( 1.0 );
				float power = pow( uWidth * 0.1, uWidth );
				float size = uSize * 0.38;
				
		 #if (COLOR_MODE == 1)
				vec3 t1 = getColor(uv, 2.), t2 = 1. - getColor(uv, -2.1);
		 #elif (COLOR_MODE == 2)
				vec3 t1 = .65 - hue(uv, 0.6, 0.005), t2 = .72 - hue(uv.yx, 0.8, 0.05);
		 #elif (COLOR_MODE == 3)
				vec3 t1 = 1. - vec3(0.1, 0.5, 0.7), t2 = 1. - vec3(0.8, 0.1, 0.8);
		 #else
				vec3 t1 = 1. - vec3(0.9, 0.5, 0.3), t2 = 1. - vec3(0.8, 0.1, 0.2);
		 #endif
		 
				float xoff = 0.5 * (0.9 * cos(iTime * 0.6 + 1.1) + 0.4 * cos(iTime * 2.4));
				for (int i = 0; i < uRingsN; i++) {
					float frac = float(i) / float(uRingsN);
					float amp = ringMultiplier(uv,
																		 0.1 + pow(frac, 3.0) * 0.7,
																		 pow(1.0 - frac, uWidth) * 0.09 + iTime * 0.0001,
																		 xoff,
																		 power);
					color *= mix(mix(t1, t2, pow(frac, 3.0)), vec3(1.0), pow(amp, 2.5));
				}
				color = 1. - color;
				gl_FragColor = vec4(color, length(color));
			}
		</script>

	</head>
	<body>
		<div id="overlay">
			<button id="startButton">Play</button>
		</div>
		<div id="container"></div>
		<script type="importmap">
			{
				"imports": {
					"three": "https://cdn.jsdelivr.net/npm/three@0.167.1/build/three.module.min.js",
					"three/addons/": "https://cdn.jsdelivr.net/npm/three@0.167.1/examples/jsm/"
				}
			}
		</script>

		<script type="module" src="main.js"></script>

	</body>
</html>
